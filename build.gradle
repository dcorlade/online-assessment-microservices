buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.5.2'
    }

}

allprojects {
    group = 'app'
    version = '0.0.1-SNAPSHOT'

}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'application'
    apply plugin: 'info.solidsoft.pitest'

    sourceCompatibility = 11
    targetCompatibility = 11

    jacocoTestCoverageVerification() {
        dependsOn test
        violationRules {
            rule {
                enabled = true
                element = 'CLASS'
                includes = ['app.*']

                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.1
                }
            }
        }
    }

    jacoco {
        toolVersion = "0.8.5"
        reportsDir = file("$buildDir/jacoco")
    }

    test {
        useJUnitPlatform()
        jacoco {
            enabled = true
            includes = ['app.*']
            excludes = []
        }
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
    }

    checkstyle {
        toolVersion "8.37"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    pmd {
        incrementalAnalysis = true
    }

    pitest {
        //adds dependency to org.pitest:pitest-junit5-plugin and sets "testPlugin" to "junit5"
        junit5PluginVersion = '0.12'

        targetClasses = ['app.*']  //by default "${project.group}.*"
        pitestVersion = '1.5.1' //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        timestampedReports = false
    }
}
